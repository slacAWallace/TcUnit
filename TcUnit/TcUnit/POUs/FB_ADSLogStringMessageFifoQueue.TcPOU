<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_ADSLogStringMessageFifoQueue" Id="{3920f5ea-d8f0-4a89-bcc8-7deea2b2fe33}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function block is responsible for making sure that the ADSLOGSTR-messages to the ADS-router are transmitted
   cyclically and not in a burst. The reason this is necessary is because that if too many messages are sent at the
   same time some get lost and are never printed to the error list output
*)
FUNCTION_BLOCK FB_ADSLogStringMessageFifoQueue
VAR
    ArrayBuffer : ARRAY[0..((RING_BUFFER_SIZE * (SIZEOF(ST_ADSLogStringMessage) + MEM_RING_BUFFER_INTERNAL_USE_PER_DATA_RECORD)) - 1)] OF BYTE;
    MemRingBuffer : Tc2_Utilities.FB_MemRingBuffer;
    TimerBetweenMessages : Tc2_Standard.TON := (IN := TRUE, PT := TIME_BETWEEN_MESSAGES);
END_VAR
VAR CONSTANT
    RING_BUFFER_SIZE : UINT := 1000;
    MEM_RING_BUFFER_INTERNAL_USE_PER_DATA_RECORD : USINT := 4;
    TIME_BETWEEN_MESSAGES : TIME := T#50MS;
END_VAR
VAR_TEMP
    MessageToBeSent : ST_ADSLogStringMessage;
    ErrorGet : BOOL;
    ReturnValue : DINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[TimerBetweenMessages();
IF GetLogCount() > 0 THEN
    // Timer has elapsed, print message
    IF TimerBetweenMessages.Q THEN
        GetLog(AdsLogStringMessage => MessageToBeSent,
               Error => ErrorGet);
        IF NOT ErrorGet THEN
            ReturnValue := Tc2_System.ADSLOGSTR(msgCtrlMask := Tc2_System.ADSLOG_MSGTYPE_ERROR,
                                                msgFmtStr := MessageToBeSent.MsgFmtStr,
                                                strArg := MessageToBeSent.StrArg);
        END_IF
        TimerBetweenMessages(IN := FALSE); // Reset timer
        TimerBetweenMessages.IN := TRUE;
    END_IF
END_IF]]></ST>
    </Implementation>
    <Method Name="GetLog" Id="{cf4219dc-c784-4d39-983c-1e64371c4d5f}">
      <Declaration><![CDATA[(* Reads and removes the oldest message *)
METHOD PRIVATE GetLog
VAR_OUTPUT
    AdsLogStringMessage : ST_ADSLogStringMessage;
    Error : BOOL; // Buffer empty
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[MemRingBuffer.A_RemoveHead(pRead := ADR(AdsLogStringMessage),
                           cbRead := SIZEOF(ST_ADSLogStringMessage),
                           pBuffer := ADR(ArrayBuffer),
                           cbBuffer := SIZEOF(ArrayBuffer));
Error := NOT MemRingBuffer.bOk;]]></ST>
      </Implementation>
    </Method>
    <Method Name="GetLogCount" Id="{b92b4756-7e23-4fd6-b9d4-ba80d89e5be5}">
      <Declaration><![CDATA[METHOD PRIVATE GetLogCount : UDINT]]></Declaration>
      <Implementation>
        <ST><![CDATA[GetLogCount := MemRingBuffer.nCount;]]></ST>
      </Implementation>
    </Method>
    <Method Name="WriteLog" Id="{4ca4d063-d389-4869-8fdb-ce7017f310dc}">
      <Declaration><![CDATA[(* Writes a new data set into the ring buffer *)
METHOD PUBLIC WriteLog
VAR_INPUT
    MsgFmtStr : Tc2_System.T_MaxString;
    StrArg : Tc2_System.T_MaxString;
END_VAR
VAR_OUTPUT
    Error : BOOL; // Buffer overflow
END_VAR
VAR
    AdsLogStringMessage : ST_ADSLogStringMessage;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[AdsLogStringMessage.MsgFmtStr := MsgFmtStr;
AdsLogStringMessage.StrArg := StrArg;

MemRingBuffer.A_AddTail(pWrite := ADR(AdsLogStringMessage),
                        cbWrite := SIZEOF(ST_ADSLogStringMessage),
                        pBuffer := ADR(ArrayBuffer),
                        cbBuffer := SIZEOF(ArrayBuffer));

Error := NOT MemRingBuffer.bOk;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_ADSLogStringMessageFifoQueue">
      <LineId Id="67" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="61" Count="0" />
      <LineId Id="63" Count="0" />
      <LineId Id="65" Count="0" />
      <LineId Id="34" Count="1" />
      <LineId Id="29" Count="0" />
      <LineId Id="66" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="23" Count="0" />
    </LineIds>
    <LineIds Name="FB_ADSLogStringMessageFifoQueue.GetLog">
      <LineId Id="9" Count="3" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ADSLogStringMessageFifoQueue.GetLogCount">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ADSLogStringMessageFifoQueue.WriteLog">
      <LineId Id="21" Count="2" />
      <LineId Id="8" Count="4" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>